/ FAKE.PA
/
/ FAKE HANDLER FOR PDP8 SIMULATOR
/ ED SMALLENBURG, 29-AUG-2017
/
        VERSION="B&77
        *0

        DECIMAL;RKLEN=3248;OCTAL
        DECIMAL;DTALEN=737;OCTAL
        DECIMAL;FLPLEN=494;OCTAL
        DECIMAL;TMPLEN=168;OCTAL

        -7                      /7 HANDLERS: SYS,RKA0,RKB0,DTA0,DTA1,RXA0,TMP0
        DEVICE FAKE;DEVICE SYS ;4231;2007;0;RKLEN
        DEVICE FAKE;DEVICE RKA0;4231;1007;0;RKLEN
        DEVICE FAKE;DEVICE RKB0;4231;RKB0H&177+1000;0;RKLEN
        DEVICE FAKE;DEVICE DTA0;4161;DTA0H&177+1000;0;DTALEN
        DEVICE FAKE;DEVICE DTA1;4161;DTA1H&177+1000;0;DTALEN
        DEVICE FAKE;DEVICE RXA0;4251;RXA0H&177+1000;0;FLPLEN
        DEVICE FAKE;DEVICE TMP0;4401;TMP0H&177+1000;0;TMPLEN
        BOOT-BLAST
        RELOC 0

BOOT,   TAD I BOOTX1
        DCA I BOOTX2
        TAD I BOOTX3
        CDF 10
        DCA I BOOTX4
        CDF 0
        TAD BOOTX2
        SZA CLA
        JMP BOOT
        JMP I B7605
BOOTX1, 177
BOOTX2, 7577
BOOTX3, 46
BOOTX4, 7646
        ZBLOCK 30-.
B7605,  7605
BLAST,  RELOC

        *200
        RELOC 7600

        ZBLOCK 7
SHNDLR, VERSION                 /SYSTEM AND RKA0: ENTRYPOINT
        CLA CLL                 /GUARD AGAINST NON-ZERO AC
        TAD SHNDLR              /POINTER TO PARAMETERS
        6770                    /SIMULATES RKA0: READ/WRITE
/NO RETURN HERE, SIMULATOR WILL RETURN TO CALLERS RETURN ADDRESS

RKB0H,  VERSION                 /RKB0: ENTRYPOINT
        CLA CLL                 /GUARD AGAINST NON-ZERO AC
        TAD RKB0H               /POINTER TO PARAMETERS
        6771                    /SIMULATES RKB0: READ/WRITE
/NO RETURN HERE

DTA0H,  VERSION                 /DTA0: ENTRYPOINT
        CLA CLL                 /GUARD AGAINST NON-ZERO AC
        TAD DTA0H               /POINTER TO PARAMETERS
        6772                    /SIMULATES DTA0: READ/WRITE
/NO RETURN HERE

DTA1H,  VERSION                 /DTA1: ENTRYPOINT
        CLA CLL                 /GUARD AGAINST NON-ZERO AC
        TAD DTA1H               /POINTER TO PARAMETERS
        6773                    /SIMULATES DTA1: READ/WRITE
/NO RETURN HERE

RXA0H,  VERSION                 /RXA0: ENTRYPOINT
        CLA CLL                 /GUARD AGAINST NON-ZERO AC
        TAD RXA0H               /POINTER TO PARAMETERS
        6774                    /SIMULATES RXA0: READ/WRITE
/NO RETURN HERE

TMP0H,  VERSION                 /TMP0: ENTRYPOINT
        CLA CLL                 /GUARD AGAINST NON-ZERO AC
        TAD TMP0H               /POINTER TO PARAMETERS
        6775                    /SIMULATES TMP0: READ/WRITE
/NO RETURN HERE
        RELOC
        $
